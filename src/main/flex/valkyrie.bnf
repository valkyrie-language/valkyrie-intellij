{
    parserClass = "valkyrie.psi.parser.YggdrasilParser"
    parserUtilClass = "valkyrie.psi.ParserExtension"

    extends = "valkyrie.psi.ValkyrieElement"

    psiClassPrefix = "Valkyrie"
    psiImplClassSuffix = "Node"
    psiPackage = "valkyrie.psi.node"
    psiImplPackage = "valkyrie.psi.node"

    elementTypeHolderClass = "valkyrie.psi.ValkyrieTypes"
    elementTypeClass = "valkyrie.psi.ValkyrieElementType"
    tokenTypeClass = "valkyrie.psi.ValkyrieTokenType"

    psiImplUtilClass = "valkyrie.psi.node.YggdrasilPsiExtension"

    tokens = [
        // comment
        COMMENT_LINE = "Comment"
        COMMENT_BLOCK = "Comment Block"

        // literal
        URL = "Url"
        SYMBOL    = "Symbol"
        ESCAPED   = "\escaped"
        INTEGER   = "<<INTEGER>>"

        REFERENCE = "$Symbol"

        SELECTION_LINE = "SELECTION_LINE"


        KW_GRAMMAR = "grammar"
        KW_USING = "using"
        KW_GROUP = "group"
        KW_CLASS = "class"
        KW_UNION = "union"
        KW_CLIMB = "climb"
        KW_IMPORT = "import"
        KW_AS = "as"
        KW_MACRO = "macro"

        PARENTHESIS_L = '('
        PARENTHESIS_R = ')'
        BRACKET_L = '['
        BRACKET_R = ']'
        BRACE_L = '{'
        BRACE_R = '}'
        ANGLE_L = '<'
        ANGLE_R = '>'
        OP_REMARK = '^'
        BIND = '='
        COLON = ':'
        SEMICOLON = ';'
        COMMA = ','
        DOLLAR = '$'
        DOT = '.'
        OP_MANY = '*'
        AT = '@'
        TO = "->"
        HYPHEN = "-"
        OP_DIV = '/'
        OP_OR = '|'
        OP_CONCAT = '~'
        PLACE_HOLDER = "_"
//      SPACE='regexp:\s+'
//      ANY = 'regexp:.'

    ]
}

// https://github.com/bytecodealliance/fluent/blob/master/spec/fluent.ebnf
root ::= statements*
private statements ::=
    declare-namespace
  | using
  | type-alias
  | trait-alias
  | declare-flags
  | declare-enumerate
  | declare-unite
  | declare-class
  | declare-union
  | declare-trait
  | declare-imply
  | declare-function
  | block-statement
private block-statement ::=
    declare-variable
  | declare-lambda
  | control-statement
  | inline-statement
private inline-statement ::=
    if-case-statement
  | if-statement
  | new-value
  | new-object
  | expression
  | SEMICOLON;
// === grammar statement ===
declare-namespace ::= annotations KW_NAMESPACE namepath-free {
	pin = 2
    mixin = "valkyrie.psi.mixin.MixinNamespace"
}
// === class statement ===
using ::= annotations KW_USING (using-body|using-term) {
    pin = 2
    mixin = "valkyrie.psi.mixin.MixinUsing"
}
using-term ::=
    using-any
  | using-alias
  | using-block
  | namepath-free
  | SEMICOLON
  | COMMA
using-any     ::= (namepath-free using-dot?) STAR using-exclude?
using-exclude ::= KW_EXCLUDE (identifier|BRACE_L (identifier (COMMA identifier)* COMMA?) BRACE_R)
using-block   ::= namepath-free using-dot? using-body
using-body    ::= BRACE_L using-term* BRACE_R;
using-alias   ::= identifier KW_AS identifier;
private using-dot ::= DOT|PROPORTION|OP_DIV
// === class statement ===
type-alias ::= annotations KW_TYPE identifier EQUAL type-expression {
    pin = 2
}
declare-class ::= annotations KW_CLASS identifier declare-generic? class-inherit-body? type-hint? class-body {
    pin = 2
    mixin = "valkyrie.psi.mixin.MixinClass"
    methods=[createLookup]
}
class-inherit-body ::= PARENTHESIS_L (class-inherit (COMMA class-inherit)? COMMA?)? PARENTHESIS_R;
class-inherit      ::= (annotations identifier EQUAL)? type-expression;
// === class body ===
class-body ::= BRACE_L class-item* BRACE_R;
class-item ::=
    declare-method
  | declare-field
  | SEMICOLON
  | COMMA

// === class field statement ===
declare-field ::= annotations identifier type-hint? default-value? {
    pin = 2
    mixin = "valkyrie.psi.mixin.MixinField"
}
type-hint     ::= COLON type-expression {
    mixin = "valkyrie.psi.mixin.MixinTypeHint"
}
default-value ::= EQUAL expression;
default-type  ::= EQUAL type-expression;
// === class method statement ===
declare-method ::= annotations identifier declare-generic? parameter-body return-type? effect-type? block-body? {
    mixin = "valkyrie.psi.mixin.MixinMethod"
}
// === union statement ===
declare-union ::= annotations KW_UNION identifier class-body {
    pin = 2
    mixin = "valkyrie.psi.mixin.MixinUnion"
    methods=[createLookup]
}
// === enumerate statement ===
declare-flags ::= annotations KW_FLAGS identifier enumerate-body {
    pin = 2
    mixin = "valkyrie.psi.mixin.MixinFlags"
    methods=[createLookup]
}
declare-enumerate ::= annotations KW_ENUMERATE identifier enumerate-body {
    pin = 2
    mixin = "valkyrie.psi.mixin.MixinEnumerate"
    methods=[createLookup]
}
enumerate-body ::= BRACE_L enumerate-item* BRACE_R;
enumerate-item ::=
    declare-semantic
  | SEMICOLON
  | COMMA
declare-semantic ::= annotations identifier default-value? {
    mixin = "valkyrie.psi.mixin.MixinSemantic"
}
// === union statement ===
declare-unite ::= annotations KW_UNITE identifier OP_REMARK? unite-body {
    pin = 2
    mixin = "valkyrie.psi.mixin.MixinUnite"
    methods=[createLookup]
}
unite-body ::= BRACE_L unite-item* BRACE_R;
unite-item ::=
    declare-variant
  | declare-method
  | declare-field
  | SEMICOLON
  | COMMA
declare-variant ::= annotations identifier class-body? {
    mixin = "valkyrie.psi.mixin.MixinVariant"
}
// === group statement ===
trait-alias ::= annotations KW_TRAIT identifier EQUAL type-expression {
    pin = 4
    mixin = "valkyrie.psi.mixin.MixinTraitAlias"
}
declare-trait ::= annotations KW_TRAIT identifier declare-generic? type-hint? class-body {
    pin = 2
    mixin = "valkyrie.psi.mixin.MixinTrait"
    methods=[createLookup]
}
declare-imply ::= annotations KW_IMPLY identifier class-body {
    pin = 2
    mixin = "valkyrie.psi.mixin.MixinImply"
}
// === generic statement ===
declare-generic ::=
    PROPORTION? GENERIC_L (generic-parameter (COMMA generic-parameter)* COMMA?)? GENERIC_R
  | PROPORTION? ANGLE_L   (generic-parameter (COMMA generic-parameter)* COMMA?)? ANGLE_R
{

}
generic-parameter ::= annotations identifier type-hint? default-type?;
/// === function definition ===
declare-function ::= annotations KW_FUNCTION declare-generic? identifier parameter-body return-type? effect-type? block-body {
    pin = 2
    mixin = "valkyrie.psi.mixin.MixinFunction"
    methods=[createLookup]
}
declare-lambda ::= KW_LAMBDA declare-generic? parameter-body return-type? effect-type? {
    pin = 1
}
parameter-body ::= PARENTHESIS_L (parameter-item (COMMA parameter-item)* COMMA?)? PARENTHESIS_R;
parameter-item ::= identifier type-hint? default-value? {

}
return-type ::= (OP_ARROW1|COLON) type-expression;
effect-type ::= SLASH type-expression;
block-body  ::= BRACE_L block-statement* BRACE_R;
block-bare  ::= block-statement*;
// === function statement ===
declare-variable ::= KW_LET pattern-bare type-hint? EQUAL inline-statement {
    pin = 1
//    mixin = "valkyrie.psi.mixin.MixinVariable"
}
// === function statement ===
function-call ::= namepath (argument-body | argument-body? block-body)?;
function-call-inline ::= namepath argument-body;
macro-call    ::= AT namepath (argument-body | argument-body? block-body)? {
    pin = 1
}
dot-call        ::= OP_AND_THEN? DOT namepath (argument-body | argument-body? block-body)?;
dot-call-inline ::= OP_AND_THEN? DOT namepath argument-body?
dot-call-for    ::= OP_AND_THEN? DOT KW_FOR pattern-bare if-condition? block-body {
    pin = 3
}
dot-call-match ::= OP_AND_THEN? DOT KW_MATCH match-body {
    pin = 3
}
// === if statement ===
if-case-statement ::= KW_IF (KW_LET|KW_CASE) pattern EQUAL inline-expression block-body {
    pin=2
}
if-statement ::= if-condition block-body else-if-statement* else-statement? {

}
if-condition ::= KW_IF inline-expression {
    pin = 1
}
else-if-statement ::= KW_ELSE KW_IF inline-expression block-body {
    pin = 2
}
else-statement ::= KW_ELSE block-body {
    pin = 1
}
// === if statement ===
while-statement ::= attribute* KW_WHILE inline-expression block-body {
    pin = 2
}
for-statement ::= attribute* KW_FOR pattern-bare KW_IN inline-expression if-condition? block-body {
    pin = 2
}
// === if statement ===
try-statement ::= KW_TRY type-expression? block-body {
    pin = 1
}
match-statement ::= KW_MATCH inline-expression match-body {
    pin = 1
}
match-body ::= BRACE_L match-item* BRACE_R;
match-item ::=
    match-with
  | match-case
  | match-when
  | match-else
match-with ::= KW_WITH pattern COLON block-bare {
    pin = 1
}
match-case ::= KW_CASE case-item COLON block-bare {
    pin = 1
}
case-item ::= namepath|number|pattern;
match-when ::= KW_WHEN inline-expression COLON block-bare {
    pin = 1
}
match-else ::= KW_ELSE COLON block-bare {
    pin = 1
}
// === pattern expression ===
pattern ::=
    pattern-unapply
  | pattern-sequence
  | pattern-object
  | pattern-literal
pattern-bare     ::=
    pattern-literal (COMMA pattern-literal)* COMMA? !(PARENTHESIS_L|BRACKET_L|BRACE_L)
  | pattern-unapply
  | pattern-sequence
  | pattern-object
pattern-unapply      ::= annotations namepath-free? pattern-unapply-body;
pattern-unapply-body ::= PARENTHESIS_L (pattern (COMMA pattern)* COMMA?)? PARENTHESIS_R {
    pin = 1
}
pattern-sequence      ::= pattern-sequence-body;
pattern-sequence-body ::= BRACKET_L (pattern (COMMA pattern)* COMMA?)? BRACKET_R {
    pin = 1
}
pattern-object      ::= annotations namepath-free? pattern-object-body;
pattern-object-body ::= BRACE_L (pattern-pair (COMMA pattern-pair)* COMMA?)? BRACE_R {
    pin = 1
}
pattern-pair      ::= annotations identifier COLON pattern;
pattern-literal   ::= annotations (identifier|string|special)
// === pattern expression ===
// new Self { r: red, g: green, b: blue, a: alpha}
new-value  ::= KW_NEW type-expression? argument-body? new-body {
    pin = 1
    mixin = "valkyrie.psi.mixin.MixinBuilder"
}
new-body   ::= BRACE_L BRACE_R;
new-object ::= KW_OBJECT class-inherit-body? type-hint? class-body {
    pin = 1
    mixin = "valkyrie.psi.mixin.MixinObject"
}
// === control expression ===
control-statement ::=
    KW_RETURN control-label? expression
  | KW_CONTINUE control-label?
  | KW_BREAK control-label?
  | KW_THROUGH control-label?
  | KW_YIELD KW_RETURN? control-label? expression
  | KW_YIELD KW_BREAK control-label?
  | KW_YIELD KW_WITH expression
  | KW_RAISE expression
  | KW_RESUME expression
control-label ::= OP_POW (identifier| INTEGER|KW_MACRO|KW_FOR|KW_WHILE);
// === common expression ===
expression ::= term (infix term)* OP_AND_THEN? {

};
term ::= prefix* atomic suffix* {

}
prefix ::= OP_PLUS | OP_MINUS | OP_BANG;
infix  ::=
    OP_PLUS | OP_MINUS
  | ANGLE_L | ANGLE_R
  | OP_AND
  | OP_UNTIL
  | OP_EQ | OP_NE
  | KW_AS
  | is_not | KW_IS
is_not ::= KW_IS KW_NOT | OP_BANG KW_IS;
suffix ::= OP_BANG
    | dot-call | dot-call-for | dot-call-match
    | generic-call;
inline-suffix ::= OP_BANG
    | dot-call-inline
    | generic-call
// === inline expression statement ===
inline-expression ::= inline-term (infix inline-term)* {

};
inline-term ::= prefix* inline-atomic inline-suffix* {

}
inline-atomic ::=
    PARENTHESIS_L expression PARENTHESIS_R
  | macro-call
  | function-call-inline
  | ordinal-range
  | offset-range
  | literal
// === expression statement ===
type-expression ::= type-term (type-infix type-term)* {
    mixin = "valkyrie.psi.mixin.MixinTypeExpression"
    methods=[highlight_trait highlight_class]
};
type-term ::= type-prefix* type-atomic type-suffix* {

}
type-prefix ::= OP_PLUS | OP_MINUS | OP_BANG;
type-infix  ::= OP_PLUS | OP_ARROW1 | OP_OR;
type-suffix ::= OP_AND_THEN | generic-call-free;
type-atomic ::= namepath | string;
// === generic statement ===
generic-call ::=
    PROPORTION? GENERIC_L (generic-argument (COMMA generic-argument)* COMMA?)? GENERIC_R
  | PROPORTION  ANGLE_L   (generic-argument (COMMA generic-argument)* COMMA?)? ANGLE_R
generic-call-free ::=
    PROPORTION? GENERIC_L (generic-argument (COMMA generic-argument)* COMMA?)? GENERIC_R
  | PROPORTION? ANGLE_L   (generic-argument (COMMA generic-argument)* COMMA?)? ANGLE_R

generic-argument ::= (annotations identifier EQUAL)? type-expression;
// === atomic statement ===
atomic ::=
    PARENTHESIS_L expression PARENTHESIS_R
  | tuple
  | macro-call
  | function-call
  | for-statement
  | while-statement
  | try-statement
  | match-statement
  | ordinal-range
  | offset-range
  | literal
// === atomic statement ===
ordinal-range ::= BRACE_L (range-item (COMMA range-item)? COMMA?) BRACE_R;
offset-range  ::= OFFSET_L (range-item (COMMA range-item)? COMMA?) OFFSET_R;
range-item ::= range-index
             | range-lower COLON range-upper?
             | range-lower COLON COLON range-step?
             | range-lower COLON range-upper COLON range-step?
             | COLON range-upper?
             | COLON COLON range-step?
             | COLON range-upper COLON range-step?
range-lower ::= expression;
range-upper ::= expression;
range-step  ::= expression;
range-index ::= expression;

tuple    ::= PARENTHESIS_L PARENTHESIS_R
  | PARENTHESIS_L argument COMMA PARENTHESIS_R
  | PARENTHESIS_L (argument (COMMA argument)+ COMMA?)? PARENTHESIS_R
{
  //    mixin = "valkyrie.psi.mixin.MixinTuple"
}
argument-body ::= PARENTHESIS_L (argument (COMMA argument)* COMMA?)? PARENTHESIS_R;
argument ::= (identifier EQUAL)? expression {

}
// === literal items ===
literal ::= special | namepath | number | string;
number ::= INTEGER;
string ::= TEXT_SINGLE | TEXT_DOUBLE {
    mixin = "valkyrie.psi.mixin.MixinString"
}
special ::= KW_NULL | KW_NIL | KW_BOOLEAN | COLOR;

// === annotation statement ===
annotations ::= attribute* modifier*
modifier    ::= (SYMBOL | SYMBOW_RAW) !modifier-stop {
    mixin = "valkyrie.psi.mixin.MixinModifier"
}
private modifier-stop ::= KW_IN | KW_IF
    | COMMA | COLON | PROPORTION | DOT | SEMICOLON | EQUAL
    | PARENTHESIS_L | PARENTHESIS_R
    | BRACE_L | BRACE_R
attribute ::= HASH namepath argument-body? {
	pin = 1
    mixin = "valkyrie.psi.mixin.MixinAttribute"
    methods=[highlight]
}
// === identifiers ===
namepath      ::= identifier (PROPORTION identifier)* {
    mixin = "valkyrie.psi.mixin.MixinNamepath"
    methods=[highlight_fake]
}
namepath-free ::= identifier ((PROPORTION|DOT) identifier)* {
    extends = namepath
}
identifier      ::= SYMBOL | SYMBOW_RAW | KW_MACRO | KW_CLASS | KW_UNION | KW_GROUP | KW_GRAMMAR | KW_IMPORT | KW_AS | KW_CLIMB {
    mixin = "valkyrie.psi.mixin.MixinIdentifier"
}
